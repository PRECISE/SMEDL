# This grammar defines PEDL in the Grako-variation of EBNF.

# Grammar directives
@@grammar :: pedl

object
    =
    'object' object:identifier
    'events'
        monitors:{new_mon}+
        event_defs:{event_def}+
    $
    ;

new_mon
    =
    'new' monitor:identifier ['(' monitor_params:identifier_list ')'] '='
        'create' struct:structure
    ;

structure
    =
    struct_id:identifier '{' fields:{field}* '}'
    ;

field
    =
    variable:identifier '=' value:expression ';'
    ;

event_def
    =
    monitor:identifier '(' monitor_params:identifier_list ').' event:identifier
        '(' [event_params:state_update_list] ')' '='
        ('update(' update_param ')' | 'call(' call_param:identifier ')')
        ['when' when:expression]
    ;

update_param
    =
      update_fn:identifier '.' update_var:identifier
    | update_var:identifier
    ;


state_update
    =
      target:target '=' expression:expression
    ;

identifier
    =
    ?/[a-zA-Z][A-Za-z0-9_]*/?
    ;

integer
    =
    ?/[-+]?[0-9]+/?
    ;

float
    =
    ?/[-+]?[0-9]*\.?[0-9]+/?
    ;

expression
    =
      or_ex:and_expr {'||' or_ex:and_expr}+
    | @:and_expr
    ;

and_expr
    =
      and_ex:sub_expr {'&&' and_ex:sub_expr}+
    | @:sub_expr
    ;

sub_expr
    =
      {'!!'}*'!(' not_ex:expression ')'
    | {'!!'}*'(' @:expression ')'
    | @:comp_expr
    ;

comp_expr
    =
      comp:arith_expr {operator:('>=' | '<=' | '==' | '!=' | '>' | '<') comp:arith_expr}+
    | '(' @:comp_expr ')'
    | @:arith_expr
    ;

arith_expr
    =
      arith:term {operator:('+' | '-' | '*' | '/' | '%') arith:term}+
    | @:term
    ;

term
    =
      {unary:('+' | '-' | '~' | '!')}* atom:atom {trailer:trailer}*
    | {unary:('+' | '-' | '~')}* '(' @:arith_expr ')'
    ;

atom
    =
      integer
    | float
    | identifier
    | 'true'
    | 'false'
    | 'null'
    ;

trailer
    =
      '[' [index:expression] ']'
    | '(' [params:expression_list] ')'
    | '.' dot:identifier {trailer:trailer}*
    ;

target
    =
    identifier
    ;

expression_list
    =
      @:expression {',' @:expression}*
    | ()
    ;

identifier_list
    =
      @:identifier {',' @:identifier}*
    | ()
    ;

state_update_list
    =
      @:state_update {',' @:state_update}*
    | ()
    ;
