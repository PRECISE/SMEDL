# This grammar defines SMEDL in the Grako-variation of EBNF.
#  written by Peter Gebhard

# Grako-specific directives
@@grammar :: smedl

object
    =
    imports+:{import_definition}*
    'object' object:identifier
    ['identity' identity+:{variable_declaration}+]
    ['state' state+:{variable_declaration}+]
    'events'
        { 'internal' internal_events+:event_definition_list
        | 'exported' exported_events+:event_definition_list}*
        'imported' imported_events+:event_definition_list
        { 'imported' imported_events+:event_definition_list
        | 'internal' internal_events+:event_definition_list
        | 'exported' exported_events+:event_definition_list}*
    'scenarios'
        scenarios+:{scenario_definition}+
    $
    ;

variable_declaration
    =
    type:type var:identifier {',' var:identifier}* ';'
    ;

import_definition
    =
    '#import' import_id:identifier
    ;

event_definition
    =
    error:['error'] event_id:identifier ['(' params:parameter_list ')'] ['=' definition:expression]
    ;

scenario_definition
    =
    atomic:['atomic'] scenario_id:identifier ':' traces:{trace_definition}+
    ;

trace_definition
    =
    start_state:identifier '->' {trace_steps+:step_definition '->'}+  end_state:identifier
          ['else' [else_actions:actions] '->' else_state:identifier]
    ;

step_definition
    =
      step_event:event_instance [step_actions:actions]
    ;

event_instance
    =
    expression:expression ['when' when:expression]
    ;

actions
    =
    '{' actions:action_item_list '}'
    ;

action_item
    =
      state_update:state_update
    | raise_stmt:raise_stmt
    | instantiation:instantiation_stmt
    | call_stmt:call_stmt
    ;

state_update
    =
      target:target operator:('++' | '--')
    | target:target operator:'=' expression:expression
    ;

raise_stmt
    =
    'raise' id:identifier ['(' expr_list+:expression_list ')']
    ;

instantiation_stmt
    =
    'new' id:identifier ['(' state_update_list+:state_update_list ')']
    ;

call_stmt
    =
      target:target '(' expr_list+:expression_list ')'
    ;

type
    =
    ?/[a-zA-Z][A-Za-z0-9_]*/?
    ;

identifier
    =
    ?/[a-zA-Z][A-Za-z0-9_]*/?
    ;

integer
    =
    ?/[-+]?[0-9]+/?
    ;

float
    =
    ?/[-+]?[0-9]*\.?[0-9]+/?
    ;

expression
    =
      or_ex:and_expr {'||' or_ex:and_expr}+
    | @:and_expr
    ;

and_expr
    =
      and_ex:sub_expr {'&&' and_ex:sub_expr}+
    | @:sub_expr
    ;

sub_expr
    =
      {'!!'}*'!(' not_ex:expression ')'
    | {'!!'}*'(' @:expression ')'
    | @:comp_expr
    ;

comp_expr
    =
      comp:arith_expr {operator:('>=' | '<=' | '==' | '!=' | '>' | '<') comp:arith_expr}+
    | '(' @:comp_expr ')'
    | @:arith_expr
    ;

arith_expr
    =
      arith:term {operator:('+' | '-' | '*' | '/' | '%') arith:term}+
    | @:term
    ;

term
    =
      {unary:('+' | '-' | '~' | '!')}* atom:atom {trailer:trailer}*
    | {unary:('+' | '-' | '~')}* '(' @:arith_expr ')'
    ;

atom
    =
      integer
    | float
    | identifier
    | 'true'
    | 'false'
    | 'null'
    ;

trailer
    =
      '[' [index:expression] ']'
    | '(' [params:expression_list] ')'
    | '.' dot:identifier {trailer:trailer}*
    ;

target
    =
    identifier
    ;

parameter_list
    =
      @:type {',' @:type}*
    | ()
    ;

expression_list
    =
      @:expression {',' @:expression}*
    | ()
    ;

event_definition_list
    =
    @:event_definition {',' @:event_definition}* ';'
    ;

identifier_list
    =
      @:identifier {',' @:identifier}*
    | ()
    ;

state_update_list
    =
      @:state_update {',' @:state_update}*
    | ()
    ;

action_item_list
    =
      @:action_item {';' @:action_item}*
    ;
