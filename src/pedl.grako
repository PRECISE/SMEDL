(*
 * This grammar defines PEDL in the Grako-variation of EBNF.
 *
 * Written February 3 2015.
 *)

monitor
    =
    monitors:{new_mon}+
    event_defs:{event_def}+
    $
    ;

new_mon
    =
    'new' monitor:identifier ['(' monitor_params:identifier_list ')'] '=' 'create' struct:structure
    ;
    
structure
    =
    struct:identifier '{' fields:{field}* '}'
    ;

field
    =
    variable:identifier '=' value:expression ';'
    ;

event_def
    =
    monitor:identifier '(' monitor_params:identifier_list ').' event:identifier ['(' event_params:identifier_list ')'] '=' 'update(' update:expression ')' ['when' when:expression] [event_action:action]
    ;
    
action
    =
    '{' actions+:nonempty_action_item_list '}'
    ;

action_item
    =
      state_update:state_update
    | raise:raise_stmt
    | instantiation:instantiation_stmt
    ;

state_update
    =
      target:target '=' expression:expression ';'
    | target:target '(' expression_list+:expression_list ')'
    ;

raise_stmt
    =
    'raise' id:identifier ['(' expr_list+:expression_list ')']
    ;

instantiation_stmt
    =
    'new' id:identifier ['(' state_update_list+:state_update_list ')']
    ;

type
    =
    ?/[a-zA-Z][A-Za-z0-9_]*/?
    ;

identifier
    =
    ?/[a-zA-Z][A-Za-z0-9_]*/?
    ;

integer
    =
    ?/[-+]?[0-9]+/?
    ;

float
    =
    ?/[-+]?[0-9]*\.?[0-9]+/?
    ;

expression
    =
      or_ex:and_expr {'||' or_ex:and_expr}+
    | @:and_expr
    ;

and_expr
    =
      and_ex:sub_expr {'&&' and_ex:sub_expr}+
    | @:sub_expr
    ;

sub_expr
    =
      '!(' not_ex:expression ')'
    | '(' @:expression ')'
    | @:comp_expr
    ;

comp_expr
    =
      comp:arith_expr {operator:('>' | '<' | '>=' | '<=' | '==') comp:arith_expr}+
    | '(' @:comp_expr ')'
    | @:arith_expr
    ;

arith_expr
    =
      arith:term {operator:('+' | '-' | '*' | '/' | '%') arith:term}+
    | @:term
    ;

term
    =
      {unary:('+' | '-' | '~')}* atom:atom {trailer:trailer}*
    | {unary:('+' | '-' | '~')}* '(' @:arith_expr ')'
    ;

atom
    =
      integer
    | float
    | identifier
    | 'true'
    | 'false'
    | 'null'
    ;

trailer
    =
      '[' [index:expression] ']'
    | '(' [params:expression_list] ')'
    | '.' dot:identifier {trailer:trailer}*
    ;
    
target
    =
    (* To be improved *)
    identifier
    ;

expression_list
    =
      @:expression {',' @:expression}*
    | ()
    ;

identifier_list
    =
      @:identifier {',' @:identifier}*
    | ()
    ;

state_update_list
    =
      @:state_update {',' @:state_update}*
    | ()
    ;

nonempty_action_item_list
    =
      @:action_item {',' @:action_item}*
    ;